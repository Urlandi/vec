;Лабораторная работа выполнена Васильевым Михаилом - ИВТ 262
;Задана строка с 1-им отличным символом. Если индекс этого символа
;больше 3 вывести строку на экран без этого символа, иначе сообщение об этом.

.MODEL TINY       ;.COM файл
.DATA             ;Сегмент данных

CSTRING DB 'ZZZZZZZZ0ZZZZZZ';Строка для обработки
STRING_LENGTH EQU $-CSTRING ;Длина этой строки
EMESS   DB 'Отличный символ имеет индекс не больше 3$' ;Сообщение для вывода

.CODE             ;Сегмент кода
.STARTUP          ;Точка фхода

        MOV DI,OFFSET CSTRING ;Адрес строки для работы
        MOV SI,DI             ;Дублируем для дальнейшей работы

        MOV CX,STRING_LENGTH  ;СХ=длина строки для работы
        MOV DX,CX             ;Дублируем для дальнейшей работы

        MOV AL,[DI]           ;В AL первый символ строки

        CLD                   ;Изменяем адреса по возрастанию
REPE    SCASB                 ;Проверка строки из ES:[DI]
                              ;на отличный от AL символ

        SUB DX,CX             ;Вычисляем индекс этого символа
        CMP DX,3              ;Если не больше 3 то
        JBE PRNMSS            ;переходим для вывода сообщения

        ADD CX,DX             ;CX=длина строки для работы
        MOV AH,[DI-1]         ;AH=отличный символ

        MOV DX,0B800H         ;Устанавливаем адрес видеопамяти
        MOV ES,DX             ;в текстовом режиме 80х25 для вывода строки

        XOR DI,DI             ;Верхний левый угол

RL:     LODSB                 ;AL=символ по адресу DS:[SI]
        CMP AL,AH             ;Если в AL отличный символ то
        JE RLC                ;не выводим его

        MOV ES:[DI],AL        ;Вывод символов
        MOV ES:[DI]+1,BYTE PTR 1BH

        INC DI                ;DI=DI+2
        INC DI
RLC:    DEC CX                ;CX=CX-1
        JNZ RL                ;Если CX не 0 повторяем иначе
        JMP STOPS             ;конец

PRNMSS: MOV AH,09H            ;9-ая функция DOS API 21h
        MOV DX,OFFSET EMESS   ;DX=адрес строки для вывода
        INT 21H               ;Выводим строку

STOPS:  XOR AH,AH             ;Ожидаем нажатия любой клавиши
        INT 16H
        RET                   ;Выход из программы
END