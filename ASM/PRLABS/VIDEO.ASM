;Лабораторная работа выполнена Васильевым Михаилом - ИВТ 262
;Задать массив из 5 элементов, найти в нем минимальный и используя видео
;память переместить его по экрану по диагонали.

VID_SEG   EQU 0B800H   ;Сегмент видео-памяти в текстовом режиме
VID_SIZE  EQU 4000     ;Его размер

        .MODEL TINY    ;.COM файл
        .DATA          ;Сегмент данных

atv     DB 3,50,13,5,7 ;Массив для анализа

        .CODE          ;Сегмент кода
        .STARTUP

;Выполняем очистку экрана
        MOV AX,0600H   ;6-ая функция видео-сервиса BIOS API
        MOV BH,07      ;Фон - черный, символ - белый
        XOR CX,CX      ;Верхний левый угол экрана
        MOV DX,184FH   ;Нижний правый угол экрана
        INT 10H        ;Вызов видео сервиса BIOS API

;Выполняем поиск минимального элемента
        LEA SI,atv     ;SI = адрес массива
        MOV CL,5       ;CL = 5
        MOV DL,[SI]    ;DL = первый элемент исходного массива

REP1:   MOV AL,[SI]    ;AL = элемент массива по адресу SI
        CMP AL,DL      ;Если AL > DL
        JG CONT1       ;не обрабатываем тело цикла

        MOV DL,AL      ;иначе DL = AL, т.е. DL - минимален

CONT1:  INC SI         ;SI = SI + 1
        DEC CL         ;CL = CL - 1
        JNZ REP1       ;Если CL = 0 завершить цикл

;Подготовка к выводу
        MOV AX,VID_SEG ;AX = адрес видео сегмента в текстовом режиме
        MOV ES,AX      ;ES = AX = адрес видео сегмента в текстовом режиме
        MOV DH,0CH     ;Фон - черный, символ - ярко-красный

        MOV AX,0720H   ;Символ для стирания - пробел, фон - черный

        MOV DI,6       ;DI = индекс 3 байта видео сегмента (выравниваем)

        XOR BX,BX      ;Определяем системную область памяти
        MOV DS,BX      ;Сегмент 0
        MOV SI,046CH   ;Адрес числа - количество тиков таймера с полуночи

;Вывод в видео память
GOGO:   MOV ES:[DI],DX ;Пишем в видео память символ из DL, c атрибутом из DH

;Задержка по времени
        MOV CL,DS:[SI] ;Младший байт загружаем в CL
        ADD CL,3       ;Промежуток времени 0.2с

STOPT:  CMP DS:[SI],CL ;Проверяем текущее время
        JNE STOPT      ;Если время не подошло повторяем проверку

        MOV ES:[DI],AX ;Стираем символ чтобы переместить его дальше

        ADD DI,166     ;Прибавляем для равномерного перемещения по диагонали
        CMP DI,VID_SIZE;Если в переделах сегмента видео памяти
        JNA GOGO       ;Повторяем операцию

        INT 20H        ;Завершение работы
END