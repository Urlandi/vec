;Лабораторная работа выполнена Васильевым Михаилом - ИВТ 262
;Массив состоит из 5 элементов, если количество 1 в максимальном
;элементе больше 0 то вывести сообщение об этом. В противном случае
;переместить положительные элементы массива в другую область памяти.

        .MODEL TINY     ;.COM файл
        .CODE
        ORG 100H        ;Обходим PSP

START:  JMP ENTRY       ;Обходим блок объявления переменных

SARRAY  DB 1,-1,2,01111100b,-15 ;Задаем исходный массив
IARRAY  DB 5 DUP (0)    ;Резервируем место для перемещения положительных

;Задаем сообщения для разных исходов программы
MSGNO   DB 'В MAX ЭЛ-ТЕ КОЛ-ВО 1 БОЛЬШЕ КОЛИЧЕСТВА 0: ПЕРЕСЫЛКА НЕ ВЫПОЛНЕНА!$'
MSGYE   DB 'В MAX ЭЛ-ТЕ КОЛ-ВО 1 НЕ БОЛЬШЕ КОЛИЧЕСТВА 0: ПЕРЕСЫЛКА ВЫПОЛНЕНА!$'

ENTRY:  MOV BX,OFFSET SARRAY ;BX = адрес исходного массива

;Находим наибольший элемент

        XOR SI,SI      ;SI = 0
        MOV CL,5       ;CL = 5
        MOV DL,[BX]    ;DL = первый элемент исходного массива

REP1:   MOV AL,[BX+SI] ;AL = элемент массива номер SI
        CMP AL,DL      ;Если AL < DL
        JL CONT1       ;не обрабатываем тело цикла

        MOV DL,AL      ;иначе DL = AL, т.е. DL - максимален

CONT1:  INC SI         ;SI = SI + 1
        DEC CL         ;CL = CL - 1
        JNZ REP1       ;Если CL = 0 завершить цикл

;Считаем количество 1 в этом элементе

        MOV AL,DL      ;AL = DL, т.е. максимальный в AL
        XOR DL,DL      ;DL = 0
        MOV CL,8       ;CL = 8

REP2:   ROR AL,1       ;Выдвигаем из AL 1 бит в CF
        JNC NONE       ;Если CF = 0 не обрабатываем тело цикла

        INC DL         ;DL = DL + 1, считаем кол-во 1

NONE:   DEC CL         ;CL = CL - 1
        JNZ REP2       ;Если CL = 0 завершить цикл

;Переходим если количество 1 больше 0

        CMP DL,4
        JA BIGER

;Выполняем пересылку

        MOV CL,5             ;CL = 5
        MOV DI,OFFSET IARRAY ;DI = адрес массива приемника
        XOR SI,SI            ;SI = 0

REP3:   MOV AL,[BX+SI] ;AL = элемент массива источника номер SI
        ADD AL,0       ;Если AL < 0
        JS CONT2       ;не обрабатываем тело цикла

        MOV [DI],AL    ;В массив приемник посылаем AL
        INC DI         ;DI = DI + 1, следующий элемент в массиве приемнике

CONT2:  INC SI         ;SI = SI + 1
        DEC CL         ;CL = CL - 1
        JNZ REP3       ;Если CL = 0 завершить цикл

        MOV DX,OFFSET MSGYE ;DX = адрес строки сообщения - 'Есть пересылка'
        JMP QUIT            ;Переход на код завершения

;Выводим сообщения

BIGER:  MOV DX,OFFSET MSGNO ;DX = адрес строки сообщения - 'Нет пересылки'

QUIT:   MOV AH,09H     ;9-ая функция DOS API INT21
        INT 21H        ;Передача управления DOS

        INT 20H        ;Завершить работу

END  START
